apply plugin: 'com.android.application'
// generate coverage while running unit tests
// (android gradle plugin only generates coverage for androidTest)
apply plugin: 'jacoco'

// Obtain verion name / string from AndroidManifest
def getMyVersionName() {
    def parsedManifestXml = (new XmlSlurper())
            .parse("${projectDir}/src/main/AndroidManifest.xml")
            .declareNamespace(android: "http://schemas.android.com/apk/res/android")
    return parsedManifestXml."@android:versionName"
}

def getMyVersionCode() {
    def parsedManifestXml = (new XmlSlurper())
            .parse("${projectDir}/src/main/AndroidManifest.xml")
            .declareNamespace(android: "http://schemas.android.com/apk/res/android")
    return parsedManifestXml."@android:versionCode".toInteger()
}

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.1"

    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "net.oldev.aDictOnCopy"
        minSdkVersion 11
        targetSdkVersion 24
        versionCode getMyVersionCode()
        versionName "${getMyVersionName()}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releaseConfig {
            if (project.hasProperty("releaseStoreFile")) {
                storeFile file(releaseStoreFile)
            } else {
                storeFile file("keystore")
            }
            if (project.hasProperty("releaseStorePassword")) {
                storePassword releaseStorePassword
            } else {
                storePassword "password"
            }
            if (project.hasProperty("releaseKeyAlias")) {
                keyAlias releaseKeyAlias
            } else {
                keyAlias "alias"
            }
            if (project.hasProperty("releaseKeyPassword")) {
                keyPassword releaseKeyPassword
            } else {
                keyPassword "password"
            }
        }
    }

    buildTypes {
        debug {
            // allow debug / release be installed as separate apps, 
            // versionNameSuffix helps to differentiate them visually
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "@string/app_name_debug"
            // testCoverageEnabled  needed for androidTest code coverage: it generates tasks
            // createDebugCoverageReport and createDebugAndroidTestCoverageReport
            // (createDebugCoverageReport only runs androidTest, as of android gradle 2.3.3)
            testCoverageEnabled true
        }
        release {
            resValue "string", "app_name", "@string/app_name_release"
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        // Needed so that Log statements using android Log won't cause unit tests fail
        unitTests.returnDefaultValues = true

        // needed for unit test code coverage with jacoco with android gradble 2.2+
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
        }
        // share codes between test and androidTest by adding sharedTest
        // to the source sets of both of them
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

}

ext {
    supportLibVersion = "25.4.0"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Upgrade support lib version to be compatible with Android Testing Support Library
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:support-annotations:$supportLibVersion"

    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.android.support.test:runner:1.0.0'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.0'
}
